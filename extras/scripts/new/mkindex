#!/usr/bin/env perl
# mkindex - restores a portable index cache to the live indexes

use strict;
use warnings;

use charnames ':full';
binmode STDOUT, ':utf8';

use File::Find;
use File::Copy;
use Term::ANSIColor qw(:constants);

if (!defined $ENV{MIBHOME}) {
  print "error: must define \$MIBHOME (where the MIB dirs live)\n";
  exit(1);
}

$ENV{SNMPCONFPATH} = '';
$ENV{SNMP_PERSISTENT_DIR} = "$ENV{MIBHOME}/extras/indexes";
$ENV{MIBS} = 'SNMPv2-MIB';
$ENV{MIBDIRS} = "$ENV{MIBHOME}/net-snmp:$ENV{MIBHOME}/rfc:$ENV{MIBHOME}/cisco";

# first setup
mkdir "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes";
mkdir "$ENV{SNMP_PERSISTENT_DIR}/cache";

# clean out any live indexes
find(sub{ -d or unlink or die $! },
  "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes");

# copy all from cache
find(sub{
  return if -d;
  &status("Copying $_");
  copy($_, "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes/$_") or die $!;
}, "$ENV{SNMP_PERSISTENT_DIR}/cache");

# restore net-snmp DIR in the index
find(sub{
  return if -d;
  &status("Relocating $_");
  system(qq(perl -pi -e 's#{{mibhome}}#$ENV{MIBHOME}#' $_)) and die ($? >> 8);
}, "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes");

# build a mib_index2.txt
open(my $index, '>', "$ENV{SNMP_PERSISTENT_DIR}/mib_index2.txt") or die $!;
print $index "MIB Index v2\n";
find(sub{
  return if -d;
  &status('Compiling mib_index2');
  print $index "\n";
  open(my $src, '<', $File::Find::name) or die $!;
  while (my $line = <$src>) { print $index $line }
  close $src;
}, "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes");
close $index;

print "\r\e[K"; # blank line
print "\N{HEAVY CHECK MARK} Indexes rebuilt.\n";
exit(0);

my $i = undef;
sub status {
  my $note = (shift || '');
  my %spinner = (
    "\N{BRAILLE PATTERN DOTS-2345678}" => "\N{BRAILLE PATTERN DOTS-1235678}",
    "\N{BRAILLE PATTERN DOTS-1235678}" => "\N{BRAILLE PATTERN DOTS-1234678}",
    "\N{BRAILLE PATTERN DOTS-1234678}" => "\N{BRAILLE PATTERN DOTS-1234578}",
    "\N{BRAILLE PATTERN DOTS-1234578}" => "\N{BRAILLE PATTERN DOTS-1234567}",
    "\N{BRAILLE PATTERN DOTS-1234567}" => "\N{BRAILLE PATTERN DOTS-1234568}",
    "\N{BRAILLE PATTERN DOTS-1234568}" => "\N{BRAILLE PATTERN DOTS-1245678}",
    "\N{BRAILLE PATTERN DOTS-1245678}" => "\N{BRAILLE PATTERN DOTS-1345678}",
    "\N{BRAILLE PATTERN DOTS-1345678}" => "\N{BRAILLE PATTERN DOTS-2345678}"
  );
  $i = (!defined $i) ? "\N{BRAILLE PATTERN DOTS-2345678}" : $spinner{$i};
  print "\r\e[K"; # blank line
  print YELLOW, "$i ", CYAN, $note, RESET;
}
