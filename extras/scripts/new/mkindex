#!/usr/bin/env perl
# mkindex - restores a portable index cache to the live indexes

use strict;
use warnings;
use File::Find;
use File::Copy;

if (!defined $ENV{MIBHOME}) {
  print "error: must define \$MIBHOME (where the MIB dirs live)\n";
  exit(1);
}

$ENV{SNMPCONFPATH} = '';
$ENV{SNMP_PERSISTENT_DIR} = "$ENV{MIBHOME}/extras/indexes";
$ENV{MIBS} = 'SNMPv2-MIB';
$ENV{MIBDIRS} = "$ENV{MIBHOME}/net-snmp:$ENV{MIBHOME}/rfc:$ENV{MIBHOME}/cisco";

# first setup
mkdir "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes";
mkdir "$ENV{SNMP_PERSISTENT_DIR}/cache";

# clean out any live indexes
find(sub{ -d or unlink or die $! },
  "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes");

# copy all from cache
find(sub{
  return if -d;
  copy($_, "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes/$_") or die $!;
}, "$ENV{SNMP_PERSISTENT_DIR}/cache");

# restore net-snmp DIR in the index
find(sub{
  return if -d;
  system(qq(perl -pi -e 's#{{mibhome}}#$ENV{MIBHOME}#' $_)) and die ($? >> 8);
}, "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes");

# build a mib_index2.txt
open(my $index, '>', "$ENV{SNMP_PERSISTENT_DIR}/mib_index2.txt") or die $!;
print $index "MIB Index v2\n";
find(sub{
  return if -d;
  print $index "\n";
  open(my $src, '<', $File::Find::name) or die $!;
  while (my $line = <$src>) { print $index $line }
  close $src;
}, "$ENV{SNMP_PERSISTENT_DIR}/mib_indexes");
close $index;

exit(0);
